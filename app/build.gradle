buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.realm:realm-gradle-plugin:2.3.1'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId 'me.vickychijwani.spectre'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2_02_02_00      // 2 digits each for major, minor, patch, build number
                                    // example: 2.0.0-beta1 => 2_00_00_00, 2.0.0 => 2_00_00_01
        versionName '2.2.2'

        // enable support for vector drawables on API < 21
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
//    signingConfigs {
//        release {
//            storeFile(file(System.getenv('KEYSTORE') ?: '') ?: '')
//            storePassword(System.getenv('KEYSTORE_PASSWORD') ?: '')
//            keyAlias(System.getenv('KEY_ALIAS') ?: '')
//            keyPassword(System.getenv('KEY_PASSWORD') ?: '')
//        }
//    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            ext.enableCrashlytics = false
        }
        release {
//            signingConfig signingConfigs.release
            minifyEnabled false   // FIXME disabled because login, revoke token strangely stopped working
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = true
        }
    }
    lintOptions {
        // check for lint errors but don't abort builds
        // I had to disable it because we don't get translations for new strings immediately
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

// don't lint for every build (run it manually when you want it to run)
tasks.whenTaskAdded { task ->
    if (task.name == 'lint') {
        task.enabled = false
    }
}

// Fabric / Crashlytics keys are mandatory for release builds
task checkFabricPropertiesExists << {
    if (!file('fabric.properties').exists()) {
        throw new FileNotFoundException('Missing fabric.properties file for Fabric / Crashlytics!')
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn checkFabricPropertiesExists
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.google.com' }
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // reactive extensions (rx)
    compile "io.reactivex.rxjava2:rxjava:2.1.4"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"

    // networking / communication / data storage
    compile("com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion") {
        exclude module: "okhttp"
    }
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofitVersion"
    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okhttpVersion"
    compile "com.squareup.picasso:picasso:2.5.2"
    compile "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0"
    compile "com.squareup:otto:1.3.8"

    // UI
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibVersion"
    compile "com.android.support:percent:$rootProject.ext.supportLibVersion"
    compile "com.android.support:customtabs:$rootProject.ext.supportLibVersion"
    compile "org.ocpsoft.prettytime:prettytime:4.0.1.Final"
    compile "com.tsengvn:typekit:1.0.1"

    // misc
    compile "com.jakewharton:butterknife:$rootProject.ext.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterKnifeVersion"
    compile("com.crashlytics.sdk.android:crashlytics:2.6.2@aar") {
        transitive = true
    }
    compile("com.crashlytics.sdk.android:answers:1.3.7@aar") {
        transitive = true
    }
    compile "com.github.hotchemi:permissionsdispatcher:$rootProject.ext.permissionsDispatcherVersion"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:$rootProject.ext.permissionsDispatcherVersion"
    compile "com.github.slugify:slugify:2.1.9"
    compile "com.jakewharton.timber:timber:4.5.1"

    // debug
    debugCompile "nl.littlerobots.rxlint:rxlint:+"
    debugCompile "com.squareup.leakcanary:leakcanary-android:1.5.4"
    debugCompile "com.jakewharton.scalpel:scalpel:1.1.2"
    debugCompile "io.palaima.debugdrawer:debugdrawer:0.7.0"
    debugCompile "io.palaima.debugdrawer:debugdrawer-commons:$rootProject.ext.debugdrawerVersion"
    debugCompile "io.palaima.debugdrawer:debugdrawer-okhttp3:$rootProject.ext.debugdrawerVersion"
    debugCompile "io.palaima.debugdrawer:debugdrawer-picasso:$rootProject.ext.debugdrawerVersion"
    debugCompile "io.palaima.debugdrawer:debugdrawer-scalpel:$rootProject.ext.debugdrawerVersion"
    debugCompile "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stethoVersion"
    debugCompile "com.uphyca:stetho_realm:2.0.0"

    // test
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "org.mockito:mockito-core:2.10.+"
    testCompile "com.squareup.retrofit2:retrofit-mock:$rootProject.ext.retrofitVersion"
    testCompile "org.json:json:20170516"    // don't depend on Android's JSONObject: http://stackoverflow.com/a/30759769/504611
    testCompile "com.squareup.okhttp3:mockwebserver:$rootProject.ext.okhttpVersion"
}
